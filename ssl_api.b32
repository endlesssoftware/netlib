%TITLE 'SSL_API'
MODULE SSL_API (IDENT='V1.0', ADDRESSING_MODE (EXTERNAL=GENERAL)) =
BEGIN
!++
! FACILITY:         NETLIB
!
! ABSTRACT:         OpenSSL API Routines
!
! MODULE DESCRIPTION:
!
!	The OpenSSL, as is provided by HP (and installed by default on
! Alpha and I64), is not backward compatible.  Every time you change
! versions of the SSL library it is necessary to relink (or re-compile).
! This can also mean that your software will stop working after an upgrade
! of the operating system.
!
! To combat this NETLIB dynamically loads the SSL routines using the routine
! LIB$FIND_IMAGE_SYMBOL (LIB$FIS).  A major effort is made to code around
! these restrictions in NETLIB, thus avoiding all the hassle associated with
! using NETLIB.
!
! AUTHOR:           Tim Sneddon
!
! Copyright (c) 2013, Endless Software Solutions.
!
! All rights reserved.
!
! Redistribution and use in source and binary forms, with or without
! modification, are permitted provided that the following conditions
! are met:
!
!     * Redistributions of source code must retain the above
!       copyright notice, this list of conditions and the following
!       disclaimer.
!     * Redistributions in binary form must reproduce the above
!       copyright notice, this list of conditions and the following
!       disclaimer in the documentation and/or other materials provided
!       with the distribution.
!     * Neither the name of the copyright owner nor the names of any
!       other contributors may be used to endorse or promote products
!       derived from this software without specific prior written
!       permission.
!
! THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
! "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
! LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
! A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
! OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
! SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
! LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
! DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
! THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
! (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
! OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
!
! CREATION DATE:    19-APR-2013
!
! MODIFICATION HISTORY:
!
!   19-APR-2013 V1.0    Sneddon     Initial coding.
!--

    LIBRARY 'SYS$LIBRARY:LIB';

    EXTERNAL ROUTINE
	LIB$FIND_IMAGE_SYMBOL;

    BIND
	SSL$LIBSSL_SHR32 = %ASCID'SSL$LIBSSL_SHR32';

    OWN
	SSL_API_INIT : INITIAL(0);

    MACRO SSL_ROUTINE(RTN) =
	GLOBAL ROUTINE RTN =
	BEGIN
	    BUILTIN
		AP, CALLG;

	    EXTERNAL ROUTINE
		SSL_library_init;

	    OWN
		%NAME('__',RTN) : INITIAL(0);

	    LOCAL
		STATUS;

	    IF (.%NAME('__',RTN) EQLA 0) THEN
	    BEGIN
		STATUS = LIB$FIND_IMAGE_SYMBOL(SSL$LIBSSL_SHR32,
					       %ASCID %STRING(RTN),
				     	       %NAME('__',RTN));
		IF (NOT .STATUS) THEN SIGNAL_STOP(.STATUS);
	    END;

	    IF (NOT .SSL_API_INIT) THEN
	    BEGIN
		SSL_API_INIT = 1;
		SSL_library_init();
	    END;

	    CALLG(.AP, .%NAME('__',RTN))
	END;
    %;

    SSL_ROUTINE(SSL_library_init);
    SSL_ROUTINE(BIO_free);
    SSL_ROUTINE(BIO_new);
    SSL_ROUTINE(BIO_s_mem);
    SSL_ROUTINE(SSLv2_method);
    SSL_ROUTINE(SSLv23_method);
    SSL_ROUTINE(SSLv3_method);
    SSL_ROUTINE(SSL_CTX_check_private_key);
    SSL_ROUTINE(SSL_CTX_free);
    SSL_ROUTINE(SSL_CTX_new);
    SSL_ROUTINE(SSL_CTX_use_PrivateKey_file);
    SSL_ROUTINE(SSL_CTX_use_certificate_file);
    SSL_ROUTINE(SSL_accept);
    SSL_ROUTINE(SSL_connect);
    SSL_ROUTINE(SSL_free);
    SSL_ROUTINE(SSL_new);
    SSL_ROUTINE(SSL_set_bio);
    SSL_ROUTINE(SSL_shutdown);
    SSL_ROUTINE(TLSv1_method);

END
ELUDOM
